if (CMAKE_VERSION VERSION_LESS 2.8.9)
    message(FATAL_ERROR "SnetDevice requires at least CMake version 2.8.9")
endif()

if (NOT SnetDevice_FIND_COMPONENTS)
    set(SnetDevice_NOT_FOUND_MESSAGE "The SnetDevice package requires at least one component")
    set(SnetDevice_FOUND False)
    return()
endif()

set(_SnetDevice_FIND_PARTS_REQUIRED)
if (SnetDevice_FIND_REQUIRED)
    set(_SnetDevice_FIND_PARTS_REQUIRED REQUIRED)
endif()
set(_SnetDevice_FIND_PARTS_QUIET)
if (SnetDevice_FIND_QUIETLY)
    set(_SnetDevice_FIND_PARTS_QUIET QUIET)
endif()

get_filename_component(_SnetDevice_install_prefix "${CMAKE_CURRENT_LIST_DIR}" ABSOLUTE)

set(_SnetDevice_NOTFOUND_MESSAGE)

# Let components find each other, but don't overwrite CMAKE_PREFIX_PATH
set(_SnetDevice_CMAKE_PREFIX_PATH_old ${CMAKE_PREFIX_PATH})
set(CMAKE_PREFIX_PATH ${_SnetDevice_install_prefix})

foreach(module ${SnetDevice_FIND_COMPONENTS})
    find_package(SnetDevice${module}
        ${_SnetDevice_FIND_PARTS_QUIET}
        ${_SnetDevice_FIND_PARTS_REQUIRED}
        PATHS "${_SnetDevice_install_prefix}" NO_DEFAULT_PATH
    )
    if (NOT SnetDevice${module}_FOUND)
        if (SnetDevice_FIND_REQUIRED_${module})
            set(_SnetDevice_NOTFOUND_MESSAGE "${_SnetDevice_NOTFOUND_MESSAGE}Failed to find SnetDevice component \"${module}\" config file at \"${_SnetDevice_install_prefix}/SnetDevice${module}/SnetDevice${module}Config.cmake\"\n")
        elseif(NOT SnetDevice_FIND_QUIETLY)
            message(WARNING "Failed to find SnetDevice component \"${module}\" config file at \"${_SnetDevice_install_prefix}/SnetDevice${module}/SnetDevice${module}Config.cmake\"")
        endif()
    endif()

    # For backward compatibility set the LIBRARIES variable
    list(APPEND SnetDevice_LIBRARIES "SnetDevice::${module}")
endforeach()

# Restore the original CMAKE_PREFIX_PATH value
set(CMAKE_PREFIX_PATH ${_SnetDevice_CMAKE_PREFIX_PATH_old})

if (_SnetDevice_NOTFOUND_MESSAGE)
    set(SnetDevice_NOT_FOUND_MESSAGE "${_SnetDevice_NOTFOUND_MESSAGE}")
    set(SnetDevice_FOUND False)
endif()

